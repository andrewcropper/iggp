;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Pilgrimage
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Components
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (role red)
  (role blue)

  (<= (base (cell ?i ?j ?h)) (index ?i) (index ?j) (height ?h))
  (<= (base (builder ?p ?i ?j)) (role ?p) (index ?i) (index ?j))
  (<= (base (pilgrim ?p ?i ?j)) (role ?p) (index ?i) (index ?j))
  (<= (base (control ?p)) (role ?p))
  (<= (base (phase ?p ?e)) (role ?p) (phase_list ?e))

  (<= (base (moves ?p ?s)) (role ?p) (succ ?s ?o))


  (<= (input ?p noop) (role ?p))

  (<= (input ?p (move ?i ?j ?m ?n))
      (role ?p)
      (index ?i)
      (index ?j)
      (index ?m)
      (index ?n))

  (<= (input ?p (raise ?i ?j))
      (role ?p)
      (index ?i)
      (index ?j))

  (<= (input ?p (place_pilgrim_action ?i ?j))
      (role ?p)
      (index ?i)
      (index ?j))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (init (builder red 2 2))
  (init (builder red 2 4))
  (init (builder red 2 5))
  (init (builder blue 5 5))
  (init (builder blue 5 2))
  (init (builder blue 5 3))
  (init (phase red build_terrain))
  (init (phase blue build_terrain))
  (init (control red))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; legal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (legal ?p noop) (role ?p) (not (has_action ?p)))
  (<= (legal ?p (place_pilgrim_action ?i ?j)) (legal_pilgrim ?p ?i ?j))
  (<= (legal ?p (move ?i ?j ?m ?n)) (legal_move ?p ?i ?j ?m ?n))
  (<= (legal ?p (raise ?i ?j)) (legal_raise ?p ?i ?j))

  (<= (has_action ?p) (legal_move ?p ?i ?j ?m ?n))
  (<= (has_action ?p) (legal_raise ?p ?i ?j))
  (<= (has_action ?p) (legal_pilgrim ?p ?i ?j))

  (<= (legal_move ?p ?i ?j ?m ?n)
      (can_move_pieces ?p)
      (piece ?p ?m ?n)
      (free_path ?i ?j ?m ?n))

  (<= (legal_raise ?p ?i ?j)
      (true (control ?p))
      (true (phase ?p build_terrain))
      (true (builder ?p ?i ?j))
      (height_end ?h)
      (not (true (cell ?i ?j ?h))))

  (<= (legal_pilgrim ?p ?i ?j)
      (true (control ?p))
      (true (phase ?p place_pilgrim))
      (true (builder ?p ?i ?j)))

  (<= (can_move_pieces ?p) (true (phase ?p build_terrain)) (not (true (control ?p))))
  (<= (can_move_pieces ?p) (true (phase ?p pilgrimage)) (true (control ?p)))

  (<= (piece ?p ?i ?j) (true (builder ?p ?i ?j)))
  (<= (piece ?p ?i ?j) (true (pilgrim ?p ?i ?j)))

  (<= (free_path ?i ?j ?m ?n)
      (adjacent ?i ?j ?m ?n)
      (true (cell ?i ?j ?h))
      (true (cell ?m ?n ?h))
      (not (filled ?i ?j)))

  (<= (free_path ?i ?j ?m ?n)
      (adjacent ?i ?j ?m ?n)
      (not (has_height ?i ?j))
      (not (has_height ?m ?n))
      (not (filled ?i ?j)))

  (<= (free_path ?i ?j ?m ?n)
      (adjacent ?i ?j ?m ?n)
      (next_height ?i ?j ?m ?n)
      (not (filled ?i ?j)))

  (<= (adjacent ?i ?j ?m ?j) (board_succ ?i ?m) (index ?j))
  (<= (adjacent ?i ?j ?m ?j) (board_succ ?m ?i) (index ?j))
  (<= (adjacent ?i ?j ?i ?n) (board_succ ?j ?n) (index ?i))
  (<= (adjacent ?i ?j ?i ?n) (board_succ ?n ?j) (index ?i))

  (<= (next_height ?i ?j ?m ?n)
      (true (cell ?i ?j ?g))
      (true (cell ?m ?n ?h))
      (height_succ ?g ?h))

  (<= (next_height ?i ?j ?m ?n)
      (true (cell ?i ?j ?g))
      (true (cell ?m ?n ?h))
      (height_succ ?h ?g))

  (<= (next_height ?i ?j ?m ?n)
      (true (cell ?m ?n 1))
      (index ?i)
      (index ?j)
      (not (has_height ?i ?j)))

  (<= (next_height ?i ?j ?m ?n)
      (true (cell ?i ?j 1))
      (index ?m)
      (index ?n)
      (not (has_height ?m ?n)))

  (<= (has_height ?i ?j) (true (cell ?i ?j ?h)))

  (<= (filled ?i ?j) (true (pilgrim ?p ?i ?j)))
  (<= (filled ?i ?j) (true (builder ?p ?i ?j)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; next
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (next (builder ?p ?i ?j)) (true (builder ?p ?i ?j)) (not (removed ?i ?j)))
  (<= (next (pilgrim ?p ?i ?j)) (true (pilgrim ?p ?i ?j)) (not (removed ?i ?j)))

  (<= (next (builder ?p ?i ?j)) (true (builder ?p ?m ?n)) (actual_move ?p ?i ?j ?m ?n))
  (<= (next (pilgrim ?p ?i ?j)) (true (pilgrim ?p ?m ?n)) (actual_move ?p ?i ?j ?m ?n))
  (<= (next (pilgrim ?p ?i ?j)) (does ?p (place_pilgrim_action ?i ?j)))

  (<= (next (phase ?p ?e)) (true (phase ?p ?e)) (not (phase_transition ?p)))
  (<= (next (control red)) (true (control blue)))
  (<= (next (control blue)) (true (control red)))


  (<= (next (cell ?i ?j 1)) (does ?p (raise ?i ?j)) (not (has_height ?i ?j)))
  (<= (next (cell ?i ?j ?h)) (true (cell ?i ?j ?h)) (not (cell_raise ?i ?j)))
  (<= (next (cell ?i ?j ?h)) (does ?p (raise ?i ?j)) (true (cell ?i ?j ?g)) (height_succ ?g ?h))

  (<= (next (moves ?p ?s)) (true (moves ?p ?o)) (succ ?s ?o))
  (<= (next (moves ?p 0)) (true (moves ?p 0)))

  (<= (next (phase ?p place_pilgrim)) (build_terrain_transition ?p))

  (<= (next (phase ?p pilgrimage)) (place_pilgrim_transition ?p))
  (<= (next (moves ?p 20)) (place_pilgrim_transition ?p))


  (<= (actual_move ?p ?i ?j ?m ?n) (does ?p (move ?i ?j ?m ?n)) (true (phase ?p pilgrimage)))
  (<= (actual_move ?p ?i ?j ?m ?n) (does ?p (move ?i ?j ?m ?n)) (true (phase ?p build_terrain)) (not (move_conflict)))

  (<= (removed ?m ?n) (actual_move ?p ?i ?j ?m ?n))
  (<= (removed ?i ?j) (does ?p (place_pilgrim_action ?i ?j)))

  (<= (move_conflict) (does ?p (move ?i ?j ?m ?n)) (does ?q (move ?i ?j ?k ?l)) (distinct ?p ?q))

  (<= (cell_raise ?i ?j) (does ?p (raise ?i ?j)))

; Phase transitions 
  (<= (phase_transition ?p) (build_terrain_transition ?p))
  (<= (build_terrain_transition ?p) (true (control ?p)) (true (phase ?p build_terrain)) (not (has_raise ?p)))

  (<= (has_raise ?p) (legal_raise ?p ?i ?j))

  (<= (phase_transition ?p) (place_pilgrim_transition ?p))
  (<= (place_pilgrim_transition ?p) (true (control ?p)) (true (phase ?p place_pilgrim)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; terminal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= terminal (complete ?p))

  (<= terminal
      (true (moves red 0))
      (true (moves blue 0))
      (true (phase ?p pilgrimage)))


  (<= (complete ?p)
      (true (phase ?p pilgrimage))
      (pilgrim_height ?p 0))


  (<= (pilgrim_height ?p 0)
      (true (pilgrim ?p ?i ?j))
      (not (has_height ?i ?j)))

  (<= (pilgrim_height ?p ?h)
      (true (pilgrim ?p ?i ?j))
      (true (cell ?i ?j ?h)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; goal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (goal red 100) (complete red) (not (complete blue)))
  (<= (goal red 50) (complete red) (complete blue))
  (<= (goal red 0) (not (complete red)) (complete blue))
  (<= (goal red 0) (not (true (phase red pilgrimage))))
  (<= (goal red ?s)
      (not (complete red))
      (not (complete blue))
      (pilgrim_height red ?h)
      (height_score ?h ?s))

  (<= (goal blue 100) (not (complete red)) (complete blue))
  (<= (goal blue 50) (complete red) (complete blue))
  (<= (goal blue 0) (complete red) (not (complete blue)))
  (<= (goal blue 0) (not (true (phase blue pilgrimage))))
  (<= (goal blue ?s)
      (not (complete red))
      (not (complete blue))
      (pilgrim_height blue ?h)
      (height_score ?h ?s))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; views
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (index 1)
  (index 2)
  (index 3)
  (index 4)
  (index 5)
  (index 6)

  (height 1)
  (height 2)
  (height 3)
  (height 4)
  (height 5)
  (height_end 5)

  (height_succ 0 1)
  (height_succ 1 2)
  (height_succ 2 3)
  (height_succ 3 4)
  (height_succ 4 5)

  (board_succ 1 2)
  (board_succ 2 3)
  (board_succ 3 4)
  (board_succ 4 5)
  (board_succ 5 6)

  (phase_list build_terrain)
  (phase_list place_pilgrim)
  (phase_list pilgrimage)

  (height_score 0 100)
  (height_score 1 40)
  (height_score 2 30)
  (height_score 3 20)
  (height_score 4 10)
  (height_score 5 0)

  (succ 0 1)
  (succ 1 2)
  (succ 2 3)
  (succ 3 4)
  (succ 4 5)
  (succ 5 6)
  (succ 6 7)
  (succ 7 8)
  (succ 8 9)
  (succ 9 10)
  (succ 10 11)
  (succ 11 12)
  (succ 12 13)
  (succ 13 14)
  (succ 14 15)
  (succ 15 16)
  (succ 16 17)
  (succ 17 18)
  (succ 18 19)
  (succ 19 20)
  (succ 20 21)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
