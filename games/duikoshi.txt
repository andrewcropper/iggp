;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Duikoshi
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Components
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (role red)
  (role green)


  (<= (base (cell ?i ?j ?x))
      (index ?i)
      (index ?j)
      (filler ?x))

  (<= (base (control ?role))
      (role ?role))



  (<= (input ?role (mark ?i ?j ?x))
      (role ?role)
      (index ?i)
      (index ?j)
      (index ?x))

  (<= (input ?role noop)
      (role ?role))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (init (cell ?i ?j b)) (index ?i) (index ?j))

  (init (control red))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; legal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (legal ?role (mark ?i ?j ?x))
      (true (control ?role))
      (true (cell ?i ?j b))
      (index ?x)
      (not (row ?i ?x))
      (not (col ?j ?x)))

  (<= (legal red noop)
      (true (control green)))

  (<= (legal green noop)
      (true (control red)))


  (<= (row ?i ?x) (true (cell ?i 1 ?x)))
  (<= (row ?i ?x) (true (cell ?i 2 ?x)))
  (<= (row ?i ?x) (true (cell ?i 3 ?x)))
  (<= (row ?i ?x) (true (cell ?i 4 ?x)))

  (<= (col ?j ?x) (true (cell 1 ?j ?x)))
  (<= (col ?j ?x) (true (cell 2 ?j ?x)))
  (<= (col ?j ?x) (true (cell 3 ?j ?x)))
  (<= (col ?j ?x) (true (cell 4 ?j ?x)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; next
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (next (cell ?i ?j ?x))
      (does ?role (mark ?i ?j ?x)))

  (<= (next (cell ?i ?j ?x))
      (true (cell ?i ?j ?x))
      (distinct ?x b))

  (<= (next (cell ?i ?j b))
      (does ?role (mark ?k ?l ?x))
      (true (cell ?i ?j b))
      (distinct ?i ?k))

  (<= (next (cell ?i ?j b))
      (does ?role (mark ?k ?l ?x))
      (true (cell ?i ?j b))
      (distinct ?j ?l))

  (<= (next (control red)) (true (control green)))
  (<= (next (control green)) (true (control red)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; goal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (goal red 100)
      (true (control green))
      open
      (not playable))

  (<= (goal green 100)
      (true (control red))
      open
      (not playable))

  (<= (goal ?role 50)
      (role ?role)
      playable)

  (<= (goal ?role 50)
      (role ?role)
      (not open))

  (<= (goal ?role 0)
      (true (control ?role))
      open
      (not playable))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; terminal
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= terminal (not playable))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Views
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (<= (filler ?x) (index ?x))
  (filler b)

  (<= open (true (cell ?i ?j b)))

  (<= playable (legal ?role (mark ?i ?j ?x)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Data
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (index 1)
  (index 2)
  (index 3)
  (index 4)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;