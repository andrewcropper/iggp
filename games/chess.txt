(role white)
(role black)
(init (cell a 1 wr))
(init (cell a 2 wp))
(init (cell a 3 b))
(init (cell a 4 b))
(init (cell a 5 b))
(init (cell a 6 b))
(init (cell a 7 bp))
(init (cell a 8 br))
(init (cell b 1 wn))
(init (cell b 2 wp))
(init (cell b 3 b))
(init (cell b 4 b))
(init (cell b 5 b))
(init (cell b 6 b))
(init (cell b 7 bp))
(init (cell b 8 bn))
(init (cell c 1 wb))
(init (cell c 2 wp))
(init (cell c 3 b))
(init (cell c 4 b))
(init (cell c 5 b))
(init (cell c 6 b))
(init (cell c 7 bp))
(init (cell c 8 bb))
(init (cell d 1 wq))
(init (cell d 2 wp))
(init (cell d 3 b))
(init (cell d 4 b))
(init (cell d 5 b))
(init (cell d 6 b))
(init (cell d 7 bp))
(init (cell d 8 bq))
(init (cell e 1 wk))
(init (cell e 2 wp))
(init (cell e 3 b))
(init (cell e 4 b))
(init (cell e 5 b))
(init (cell e 6 b))
(init (cell e 7 bp))
(init (cell e 8 bk))
(init (cell f 1 wb))
(init (cell f 2 wp))
(init (cell f 3 b))
(init (cell f 4 b))
(init (cell f 5 b))
(init (cell f 6 b))
(init (cell f 7 bp))
(init (cell f 8 bb))
(init (cell g 1 wn))
(init (cell g 2 wp))
(init (cell g 3 b))
(init (cell g 4 b))
(init (cell g 5 b))
(init (cell g 6 b))
(init (cell g 7 bp))
(init (cell g 8 bn))
(init (cell h 1 wr))
(init (cell h 2 wp))
(init (cell h 3 b))
(init (cell h 4 b))
(init (cell h 5 b))
(init (cell h 6 b))
(init (cell h 7 bp))
(init (cell h 8 br))
(init (control white))
(init (step 1))
(<= (next (cell ?u ?v b))
    (does ?player (move ?p ?u ?v ?x ?y)))
(<= (next (cell ?x ?y ?p))
    (does ?player (move ?p ?u ?v ?x ?y))
    (or (distinct ?p wp) (distinct ?y 8))
    (or (distinct ?p bp) (distinct ?y 1)))
(<= (next (cell ?x ?y ?p))
    (does ?player (move ?piece ?x1 ?y1 ?x2 ?y2))
    (true (cell ?x ?y ?p))
    (not (is_castle_move ?piece ?x1 ?y1 ?x2 ?y2))
    (not (pawn_capture_en_passant ?player ?x1 ?y1 ?x2 ?y2))
    (different_cells ?x ?y ?x1 ?y1)
    (different_cells ?x ?y ?x2 ?y2))
(<= (next (control white))
    (true (control black)))
(<= (next (control black))
    (true (control white)))
(<= (next (step ?y))
    (true (step ?x))
    (succ ?x ?y))
(<= (next (cell ?x 8 wq))
    (does ?player (move wp ?u ?v ?x 8)))
(<= (next (cell ?x 1 bq))
    (does ?player (move bp ?u ?v ?x 1)))
(<= (next (cell ?x ?y ?p))
    (does ?player (move ?q ?x1 ?y1 ?x2 ?y2))
    (true (cell ?x ?y ?p))
    (is_castle_move ?q ?x1 ?y1 ?x2 ?y2)
    (not (is_castle_cell ?x2 ?y2 ?x ?y)))
(<= (next (cell ?x ?y ?p))
    (does ?player (move ?piece ?x1 ?y1 ?x2 ?y2))
    (true (cell ?x ?y ?p))
    (pawn_capture_en_passant ?player ?x1 ?y1 ?x2 ?y2)
    (different_cells ?x ?y ?x1 ?y1)
    (different_cells ?x ?y ?x2 ?y2)
    (different_cells ?x ?y ?x2 ?y1))
(<= (next (cell ?x2 ?y1 b))
    (does ?player (move ?piece ?x1 ?y1 ?x2 ?y2))
    (pawn_capture_en_passant ?player ?x1 ?y1 ?x2 ?y2))
(<= (next (cell e 1 b))
    (does white (move wk e 1 g 1)))
(<= (next (cell f 1 wr))
    (does white (move wk e 1 g 1)))
(<= (next (cell g 1 wk))
    (does white (move wk e 1 g 1)))
(<= (next (cell h 1 b))
    (does white (move wk e 1 g 1)))
(<= (next (cell a 1 b))
    (does white (move wk e 1 c 1)))
(<= (next (cell b 1 b))
    (does white (move wk e 1 c 1)))
(<= (next (cell c 1 wk))
    (does white (move wk e 1 c 1)))
(<= (next (cell d 1 wr))
    (does white (move wk e 1 c 1)))
(<= (next (cell e 1 b))
    (does white (move wk e 1 c 1)))
(<= (next (cell e 8 b))
    (does black (move bk e 8 g 8)))
(<= (next (cell f 8 br))
    (does black (move bk e 8 g 8)))
(<= (next (cell g 8 bk))
    (does black (move bk e 8 g 8)))
(<= (next (cell h 8 b))
    (does black (move bk e 8 g 8)))
(<= (next (cell a 8 b))
    (does black (move bk e 8 c 8)))
(<= (next (cell b 8 b))
    (does black (move bk e 8 c 8)))
(<= (next (cell c 8 bk))
    (does black (move bk e 8 c 8)))
(<= (next (cell d 8 br))
    (does black (move bk e 8 c 8)))
(<= (next (cell e 8 b))
    (does black (move bk e 8 c 8)))
(<= (next (piece_has_moved wk e 1))
    (does white (move wk e 1 ?x ?y)))
(<= (next (piece_has_moved wr a 1))
    (does white (move wr a 1 ?x ?y)))
(<= (next (piece_has_moved wr h 1))
    (does white (move wr h 1 ?x ?y)))
(<= (next (piece_has_moved bk e 8))
    (does black (move bk e 8 ?x ?y)))
(<= (next (piece_has_moved br a 8))
    (does black (move br a 8 ?x ?y)))
(<= (next (piece_has_moved br h 8))
    (does black (move br h 8 ?x ?y)))
(<= (next (piece_has_moved ?p ?x ?y))
    (true (piece_has_moved ?p ?x ?y)))
(<= (next (pawn_moved_two wp ?x))
    (does white (move wp ?x 2 ?x 4)))
(<= (next (pawn_moved_two bp ?x))
    (does black (move bp ?x 7 ?x 5)))
(<= (next (check ?player pawn ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x ?y))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent pawn)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (pawn_capture ?x ?y ?kx ?ky ?opponent))
(<= (next (check ?player knight ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x ?y))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent knight)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (knight_move ?piece ?x ?y ?kx ?ky ?opponent))
(<= (next (check ?player rook ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x ?y))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent rook)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (rook_move ?x ?y ?kx ?ky))
(<= (next (check ?player bishop ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x ?y))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent bishop)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (bishop_move ?x ?y ?kx ?ky))
(<= (next (check ?player queen ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x ?y))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent queen)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (queen_move ?x ?y ?kx ?ky))
(<= (next (check ?player rook ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x1 ?y1))
    (opponent ?player ?opponent)
    (piece_owner_type ?rook ?opponent rook)
    (piece_owner_type ?king ?player king)
    (true (cell ?x ?y ?rook))
    (true (cell ?kx ?ky ?king))
    (rook_threatens ?opponent ?x ?y ?kx ?ky ?u ?v))
(<= (next (check ?player bishop ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x1 ?y1))
    (opponent ?player ?opponent)
    (piece_owner_type ?bishop ?opponent bishop)
    (piece_owner_type ?king ?player king)
    (true (cell ?x ?y ?bishop))
    (true (cell ?kx ?ky ?king))
    (bishop_threatens ?opponent ?x ?y ?kx ?ky ?u ?v))
(<= (next (check ?player queen ?x ?y))
    (does ?opponent (move ?piece ?u ?v ?x1 ?y1))
    (opponent ?player ?opponent)
    (piece_owner_type ?queen ?opponent queen)
    (piece_owner_type ?king ?player king)
    (true (cell ?x ?y ?queen))
    (true (cell ?kx ?ky ?king))
    (queen_threatens ?opponent ?x ?y ?kx ?ky ?u ?v))
(<= (legal ?player (move ?piece ?u ?v ?x ?y))
    (true (control ?player))
    (not (in_check ?player))
    (piece_owner_type ?piece ?player ?ptype)
    (distinct ?ptype king)
    (true (cell ?u ?v ?piece))
    (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (true (cell ?x ?y b))
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (not (threatened ?player ?kx ?ky ?u ?v)))
(<= (legal ?player (move ?piece ?u ?v ?x ?y))
    (true (control ?player))
    (not (in_check ?player))
    (piece_owner_type ?piece ?player ?ptype)
    (distinct ?ptype king)
    (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (occupied_by_opponent ?x ?y ?player)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (not (threatened_with_capture ?player ?x ?y ?kx ?ky ?u ?v)))
(<= (legal ?player (move ?king ?u ?v ?x ?y))
    (true (control ?player))
    (piece_owner_type ?king ?player king)
    (true (cell ?u ?v ?king))
    (kingmove ?u ?v ?x ?y)
    (occupied_by_opponent_or_blank ?x ?y ?player)
    (not (threatened ?player ?x ?y ?u ?v)))
(<= (legal ?player (move ?piece ?u ?v ?x ?y))
    (true (control ?player))
    (true (check ?player ?type ?x ?y))
    (occupied_by_opponent ?x ?y ?player)
    (piece_owner_type ?piece ?player ?ptype)
    (distinct ?ptype king)
    (true (cell ?u ?v ?piece))
    (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (not (threatened_with_capture ?player ?x ?y ?kx ?ky ?u ?v)))
(<= (legal ?player (move ?piece ?u ?v ?x ?y))
    (true (control ?player))
    (or (true (check ?player rook ?tx ?ty)) (true (check ?player queen ?tx ?ty)))
    (not (occupied_by_player ?x ?y ?player))
    (piece_owner_type ?piece ?player ?ptype)
    (distinct ?ptype king)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (blocks_rook_threat ?x ?y ?tx ?ty ?kx ?ky)
    (not (threatened_with_capture ?player ?tx ?ty ?kx ?ky ?u ?v)))
(<= (legal ?player (move ?piece ?u ?v ?x ?y))
    (true (control ?player))
    (or (true (check ?player bishop ?tx ?ty)) (true (check ?player queen ?tx ?ty)))
    (not (occupied_by_player ?x ?y ?player))
    (piece_owner_type ?piece ?player ?ptype)
    (distinct ?ptype king)
    (piece_owner_type ?king ?player king)
    (true (cell ?kx ?ky ?king))
    (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (blocks_bishop_threat ?x ?y ?tx ?ty ?kx ?ky)
    (not (threatened_with_capture ?player ?tx ?ty ?kx ?ky ?u ?v)))
(<= (blocks_rook_threat ?x ?y ?tx ?y ?kx ?y)
    (rook_move ?tx ?y ?x ?y)
    (rook_move ?x ?y ?kx ?y))
(<= (blocks_rook_threat ?x ?y ?x ?ty ?x ?ky)
    (rook_move ?x ?ty ?x ?y)
    (rook_move ?x ?y ?x ?ky))
(<= (blocks_bishop_threat ?x ?y ?tx ?ty ?kx ?ky)
    (clear_diagonal_ne ?tx ?ty ?x ?y)
    (clear_diagonal_ne ?x ?y ?kx ?ky))
(<= (blocks_bishop_threat ?x ?y ?tx ?ty ?kx ?ky)
    (clear_diagonal_ne ?kx ?ky ?x ?y)
    (clear_diagonal_ne ?x ?y ?tx ?ty))
(<= (blocks_bishop_threat ?x ?y ?tx ?ty ?kx ?ky)
    (clear_diagonal_se ?tx ?ty ?x ?y)
    (clear_diagonal_se ?x ?y ?kx ?ky))
(<= (blocks_bishop_threat ?x ?y ?tx ?ty ?kx ?ky)
    (clear_diagonal_se ?kx ?ky ?x ?y)
    (clear_diagonal_se ?x ?y ?tx ?ty))
(<= (legal white (move wk e 1 g 1))
    (true (control white))
    (true (cell e 1 wk))
    (true (cell f 1 b))
    (true (cell g 1 b))
    (true (cell h 1 wr))
    (not (true (piece_has_moved wk e 1)))
    (not (true (piece_has_moved wr h 1)))
    (not (in_check white))
    (not (threatened white f 1 e 1))
    (not (threatened white g 1 e 1)))
(<= (legal white (move wk e 1 c 1))
    (true (control white))
    (true (cell a 1 wr))
    (true (cell b 1 b))
    (true (cell c 1 b))
    (true (cell d 1 b))
    (true (cell e 1 wk))
    (not (true (piece_has_moved wr a 1)))
    (not (true (piece_has_moved wk e 1)))
    (not (in_check white))
    (not (threatened white d 1 e 1))
    (not (threatened white c 1 e 1)))
(<= (legal black (move bk e 8 g 8))
    (true (control black))
    (true (cell e 8 bk))
    (true (cell f 8 b))
    (true (cell g 8 b))
    (true (cell h 8 br))
    (not (true (piece_has_moved bk e 8)))
    (not (true (piece_has_moved br h 8)))
    (not (in_check black))
    (not (threatened black f 8 e 8))
    (not (threatened black g 8 e 8)))
(<= (legal black (move bk e 8 c 8))
    (true (control black))
    (true (cell a 8 br))
    (true (cell b 8 b))
    (true (cell c 8 b))
    (true (cell d 8 b))
    (true (cell e 8 bk))
    (not (true (piece_has_moved br a 8)))
    (not (true (piece_has_moved bk e 8)))
    (not (in_check black))
    (not (threatened black d 8 e 8))
    (not (threatened black c 8 e 8)))
(<= (legal white noop)
    (true (control black)))
(<= (legal black noop)
    (true (control white)))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player king)
    (true (cell ?u ?v ?piece))
    (kingmove ?u ?v ?x ?y))
(<= (kingmove ?u ?v ?u ?y)
    (adjacent ?v ?y)
    (coordinate ?u))
(<= (kingmove ?u ?v ?x ?v)
    (adjacent ?u ?x)
    (coordinate ?v))
(<= (kingmove ?u ?v ?x ?y)
    (adjacent ?u ?x)
    (adjacent ?v ?y))
(<= (king_threatens ?attacker ?x ?y)
    (piece_owner_type ?piece ?attacker king)
    (true (cell ?u ?v ?piece))
    (kingmove ?u ?v ?x ?y))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player queen)
    (true (cell ?u ?v ?piece))
    (queen_move ?u ?v ?x ?y))
(<= (queen_move ?u ?v ?x ?y)
    (bishop_move ?u ?v ?x ?y))
(<= (queen_move ?u ?v ?x ?y)
    (rook_move ?u ?v ?x ?y))
(<= (queen_threatens ?attacker ?u ?v ?x ?y ?ignorex ?ignorey)
    (piece_owner_type ?piece ?attacker queen)
    (true (cell ?u ?v ?piece))
    (queen_move_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (queen_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (queen_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player rook)
    (true (cell ?u ?v ?piece))
    (rook_move ?u ?v ?x ?y))
(<= (rook_move ?u ?v ?u ?y)
    (clear_column ?u ?v ?y))
(<= (rook_move ?u ?v ?u ?y)
    (clear_column ?u ?y ?v))
(<= (rook_move ?u ?v ?x ?v)
    (clear_row ?u ?x ?v))
(<= (rook_move ?u ?v ?x ?v)
    (clear_row ?x ?u ?v))
(<= (rook_threatens ?attacker ?u ?v ?x ?y ?ignorex ?ignorey)
    (piece_owner_type ?rook ?attacker rook)
    (true (cell ?u ?v ?rook))
    (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_row_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_row_ig ?x ?y ?u ?v ?ignorex ?ignorey))
(<= (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_col_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (rook_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_col_ig ?x ?y ?u ?v ?ignorex ?ignorey))
(<= (clear_row_ig ?u ?y ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?x)
    (coordinate ?y)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (clear_row_ig ?u ?ignorey ?x ?ignorey ?ignorex ?ignorey)
    (next_file ?u ?ignorex)
    (clear_row_ig ?ignorex ?ignorey ?x ?ignorey ?ignorex ?ignorey))
(<= (clear_row_ig ?u ?y ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?u2)
    (true (cell ?u2 ?y b))
    (clear_row_ig ?u2 ?y ?x ?y ?ignorex ?ignorey))
(<= (clear_col_ig ?u ?v ?u ?y ?ignorex ?ignorey)
    (next_rank ?v ?y)
    (coordinate ?u)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (clear_col_ig ?ignorex ?v ?ignorex ?y ?ignorex ?ignorey)
    (next_rank ?v ?ignorey)
    (clear_col_ig ?ignorex ?ignorey ?ignorex ?y ?ignorex ?ignorey))
(<= (clear_col_ig ?u ?v ?u ?y ?ignorex ?ignorey)
    (next_rank ?v ?v2)
    (true (cell ?u ?v2 b))
    (clear_col_ig ?u ?v2 ?u ?y ?ignorex ?ignorey))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (role ?player)
    (piece_owner_type ?piece ?player bishop)
    (true (cell ?u ?v ?piece))
    (bishop_move ?u ?v ?x ?y))
(<= (bishop_move ?u ?v ?x ?y)
    (clear_diagonal_ne ?u ?v ?x ?y))
(<= (bishop_move ?u ?v ?x ?y)
    (clear_diagonal_ne ?x ?y ?u ?v))
(<= (bishop_move ?u ?v ?x ?y)
    (clear_diagonal_se ?u ?v ?x ?y))
(<= (bishop_move ?u ?v ?x ?y)
    (clear_diagonal_se ?x ?y ?u ?v))
(<= (clear_diagonal_ne ?u ?v ?x ?y)
    (next_file ?u ?x)
    (next_rank ?v ?y))
(<= (clear_diagonal_ne ?u ?v ?x ?y)
    (next_file ?u ?u2)
    (next_rank ?v ?v2)
    (true (cell ?u2 ?v2 b))
    (clear_diagonal_ne ?u2 ?v2 ?x ?y))
(<= (clear_diagonal_se ?u ?v ?x ?y)
    (next_file ?u ?x)
    (next_rank ?y ?v))
(<= (clear_diagonal_se ?u ?v ?x ?y)
    (next_file ?u ?u2)
    (next_rank ?v2 ?v)
    (true (cell ?u2 ?v2 b))
    (clear_diagonal_se ?u2 ?v2 ?x ?y))
(<= (bishop_threatens ?attacker ?u ?v ?x ?y ?ignorex ?ignorey)
    (piece_owner_type ?bishop ?attacker bishop)
    (true (cell ?u ?v ?bishop))
    (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_diagonal_ne_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_diagonal_ne_ig ?x ?y ?u ?v ?ignorex ?ignorey))
(<= (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_diagonal_se_ig ?u ?v ?x ?y ?ignorex ?ignorey))
(<= (bishop_move_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (clear_diagonal_se_ig ?x ?y ?u ?v ?ignorex ?ignorey))
(<= (clear_diagonal_ne_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?x)
    (next_rank ?v ?y)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (clear_diagonal_ne_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?ignorex)
    (next_rank ?v ?ignorey)
    (clear_diagonal_ne_ig ?ignorex ?ignorey ?x ?y ?ignorex ?ignorey))
(<= (clear_diagonal_ne_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?u2)
    (next_rank ?v ?v2)
    (true (cell ?u2 ?v2 b))
    (clear_diagonal_ne_ig ?u2 ?v2 ?x ?y ?ignorex ?ignorey))
(<= (clear_diagonal_se_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?x)
    (next_rank ?y ?v)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (clear_diagonal_se_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?ignorex)
    (next_rank ?ignorey ?v)
    (clear_diagonal_se_ig ?ignorex ?ignorey ?x ?y ?ignorex ?ignorey))
(<= (clear_diagonal_se_ig ?u ?v ?x ?y ?ignorex ?ignorey)
    (next_file ?u ?u2)
    (next_rank ?v2 ?v)
    (true (cell ?u2 ?v2 b))
    (clear_diagonal_se_ig ?u2 ?v2 ?x ?y ?ignorex ?ignorey))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player knight)
    (true (cell ?u ?v ?piece))
    (knight_move ?piece ?u ?v ?x ?y ?player))
(<= (knight_move ?piece ?u ?v ?x ?y ?owner)
    (piece_owner_type ?piece ?owner knight)
    (adjacent_two ?v ?y)
    (adjacent ?u ?x))
(<= (knight_move ?piece ?u ?v ?x ?y ?owner)
    (piece_owner_type ?piece ?owner knight)
    (adjacent_two ?u ?x)
    (adjacent ?v ?y))
(<= (knight_threatens ?attacker ?x ?y)
    (piece_owner_type ?piece ?attacker knight)
    (true (cell ?u ?v ?piece))
    (knight_move ?piece ?u ?v ?x ?y ?attacker))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player pawn)
    (true (cell ?u ?v ?piece))
    (pawn_advance ?u ?v ?x ?y ?player)
    (true (cell ?x ?y b)))
(<= (pawn_advance ?u ?v ?u ?y white)
    (next_rank ?v ?y)
    (coordinate ?u))
(<= (pawn_advance ?u 2 ?u 4 white)
    (true (cell ?u 3 b)))
(<= (pawn_advance ?u ?v ?u ?y black)
    (next_rank ?y ?v)
    (coordinate ?u))
(<= (pawn_advance ?u 7 ?u 5 black)
    (true (cell ?u 6 b)))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player pawn)
    (true (cell ?u ?v ?piece))
    (pawn_capture ?u ?v ?x ?y ?player)
    (true (cell ?x ?y ?captured))
    (piece_owner_type ?captured ?opponent ?type)
    (opponent ?player ?opponent))
(<= (pawn_capture ?u ?v ?x ?y white)
    (next_rank ?v ?y)
    (or (next_file ?u ?x) (next_file ?x ?u)))
(<= (pawn_capture ?u ?v ?x ?y black)
    (next_rank ?y ?v)
    (or (next_file ?u ?x) (next_file ?x ?u)))
(<= (legal2 ?player (move ?piece ?u ?v ?x ?y))
    (piece_owner_type ?piece ?player pawn)
    (true (cell ?u ?v ?piece))
    (pawn_capture_en_passant ?player ?u ?v ?x ?y))
(<= (pawn_capture_en_passant white ?x1 5 ?x2 6)
    (true (cell ?x1 5 wp))
    (true (pawn_moved_two bp ?x2))
    (pawn_capture ?x1 5 ?x2 6 white))
(<= (pawn_capture_en_passant black ?x1 4 ?x2 3)
    (true (cell ?x1 4 bp))
    (true (pawn_moved_two wp ?x2))
    (pawn_capture ?x1 4 ?x2 3 black))
(<= (threatened ?owner ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (piece_owner_type ?opiece ?opponent pawn)
    (true (cell ?u ?v ?opiece))
    (pawn_capture ?u ?v ?x ?y ?opponent)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (threatened ?owner ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (or (bishop_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey) (rook_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey) (queen_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey) (knight_threatens ?opponent ?x ?y) (king_threatens ?opponent ?x ?y))
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (threatened_with_capture ?owner ?cx ?cy ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (piece_owner_type ?opiece ?opponent pawn)
    (true (cell ?u ?v ?opiece))
    (different_cells ?cx ?cy ?u ?v)
    (pawn_capture ?u ?v ?x ?y ?opponent)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (threatened_with_capture ?owner ?cx ?cy ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (or (bishop_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey) (rook_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey) (queen_threatens ?opponent ?u ?v ?x ?y ?ignorex ?ignorey))
    (different_cells ?cx ?cy ?u ?v))
(<= (threatened_with_capture ?owner ?cx ?cy ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (piece_owner_type ?piece ?opponent knight)
    (true (cell ?u ?v ?piece))
    (different_cells ?cx ?cy ?u ?v)
    (knight_move ?piece ?u ?v ?x ?y ?opponent)
    (coordinate ?ignorex)
    (coordinate ?ignorey))
(<= (threatened_with_capture ?owner ?cx ?cy ?x ?y ?ignorex ?ignorey)
    (opponent ?owner ?opponent)
    (king_threatens ?opponent ?x ?y)
    (coordinate ?ignorex)
    (coordinate ?ignorey)
    (coordinate ?cx)
    (coordinate ?cy))
(<= (adjacent ?x1 ?x2)
    (next_file ?x1 ?x2))
(<= (adjacent ?x1 ?x2)
    (next_file ?x2 ?x1))
(<= (adjacent ?y1 ?y2)
    (next_rank ?y1 ?y2))
(<= (adjacent ?y1 ?y2)
    (next_rank ?y2 ?y1))
(<= (adjacent_two ?a ?b)
    (adjacent ?a ?mid)
    (adjacent ?mid ?b)
    (distinct ?a ?b))
(<= (clear_column ?u ?v ?y)
    (next_rank ?v ?y)
    (coordinate ?u))
(<= (clear_column ?u ?v ?y)
    (next_rank ?v ?w)
    (true (cell ?u ?w b))
    (clear_column ?u ?w ?y))
(<= (clear_row ?u ?x ?v)
    (next_file ?u ?x)
    (coordinate ?v))
(<= (clear_row ?u ?x ?v)
    (next_file ?u ?w)
    (true (cell ?w ?v b))
    (clear_row ?w ?x ?v))
(<= (different_cells ?x1 ?y1 ?x2 ?y2)
    (distinct ?x1 ?x2)
    (coordinate ?x1)
    (coordinate ?x2)
    (coordinate ?y1)
    (coordinate ?y2))
(<= (different_cells ?x1 ?y1 ?x2 ?y2)
    (distinct ?y1 ?y2)
    (coordinate ?x1)
    (coordinate ?x2)
    (coordinate ?y1)
    (coordinate ?y2))
(<= (has_legal_move ?player)
    (legal ?player (move ?piece ?u ?v ?x ?y)))
(<= (in_check ?player)
    (true (check ?player ?ptype ?x ?y)))
(<= (occupied_by_opponent ?x ?y ?player)
    (true (cell ?x ?y ?piece))
    (opponent ?player ?opponent)
    (piece_owner_type ?piece ?opponent ?type))
(<= (occupied_by_player ?x ?y ?player)
    (true (cell ?x ?y ?piece))
    (piece_owner_type ?piece ?player ?type))
(<= (occupied_by_opponent_or_blank ?x ?y ?player)
    (true (cell ?x ?y b))
    (role ?player))
(<= (occupied_by_opponent_or_blank ?x ?y ?player)
    (occupied_by_opponent ?x ?y ?player))
(<= (checkmate ?player)
    (true (control ?player))
    (true (check ?player ?ptype ?x ?y))
    (stuck ?player))
(<= stalemate
    (true (control ?player))
    (not (in_check ?player))
    (stuck ?player))
(<= (stuck ?player)
    (role ?player)
    (not (has_legal_move ?player)))
(<= (goal white 100)
    (checkmate black))
(<= (goal white 50)
    stalemate)
(<= (goal white 0)
    (checkmate white))
(<= (goal black 100)
    (checkmate white))
(<= (goal black 50)
    stalemate)
(<= (goal black 0)
    (checkmate black))
(<= terminal
    (true (step 201)))
(<= terminal
    (true (control ?player))
    (stuck ?player))
(opponent white black)
(opponent black white)
(piece_owner_type wk white king)
(piece_owner_type wq white queen)
(piece_owner_type wr white rook)
(piece_owner_type wb white bishop)
(piece_owner_type wn white knight)
(piece_owner_type wp white pawn)
(piece_owner_type bk black king)
(piece_owner_type bq black queen)
(piece_owner_type br black rook)
(piece_owner_type bb black bishop)
(piece_owner_type bn black knight)
(piece_owner_type bp black pawn)
(is_castle_cell g 1 e 1)
(is_castle_cell g 1 f 1)
(is_castle_cell g 1 g 1)
(is_castle_cell g 1 h 1)
(is_castle_cell c 1 a 1)
(is_castle_cell c 1 b 1)
(is_castle_cell c 1 c 1)
(is_castle_cell c 1 d 1)
(is_castle_cell c 1 e 1)
(is_castle_cell g 8 e 8)
(is_castle_cell g 8 f 8)
(is_castle_cell g 8 g 8)
(is_castle_cell g 8 h 8)
(is_castle_cell c 8 a 8)
(is_castle_cell c 8 b 8)
(is_castle_cell c 8 c 8)
(is_castle_cell c 8 d 8)
(is_castle_cell c 8 e 8)
(is_castle_move wk e 1 c 1)
(is_castle_move wk e 1 g 1)
(is_castle_move bk e 8 c 8)
(is_castle_move bk e 8 g 8)
(next_rank 1 2)
(next_rank 2 3)
(next_rank 3 4)
(next_rank 4 5)
(next_rank 5 6)
(next_rank 6 7)
(next_rank 7 8)
(next_file a b)
(next_file b c)
(next_file c d)
(next_file d e)
(next_file e f)
(next_file f g)
(next_file g h)
(coordinate 1)
(coordinate 2)
(coordinate 3)
(coordinate 4)
(coordinate 5)
(coordinate 6)
(coordinate 7)
(coordinate 8)
(coordinate a)
(coordinate b)
(coordinate c)
(coordinate d)
(coordinate e)
(coordinate f)
(coordinate g)
(coordinate h)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)
(succ 64 65)
(succ 65 66)
(succ 66 67)
(succ 67 68)
(succ 68 69)
(succ 69 70)
(succ 70 71)
(succ 71 72)
(succ 72 73)
(succ 73 74)
(succ 74 75)
(succ 75 76)
(succ 76 77)
(succ 77 78)
(succ 78 79)
(succ 79 80)
(succ 80 81)
(succ 81 82)
(succ 82 83)
(succ 83 84)
(succ 84 85)
(succ 85 86)
(succ 86 87)
(succ 87 88)
(succ 88 89)
(succ 89 90)
(succ 90 91)
(succ 91 92)
(succ 92 93)
(succ 93 94)
(succ 94 95)
(succ 95 96)
(succ 96 97)
(succ 97 98)
(succ 98 99)
(succ 99 100)
(succ 100 101)
(succ 101 102)
(succ 102 103)
(succ 103 104)
(succ 104 105)
(succ 105 106)
(succ 106 107)
(succ 107 108)
(succ 108 109)
(succ 109 110)
(succ 110 111)
(succ 111 112)
(succ 112 113)
(succ 113 114)
(succ 114 115)
(succ 115 116)
(succ 116 117)
(succ 117 118)
(succ 118 119)
(succ 119 120)
(succ 120 121)
(succ 121 122)
(succ 122 123)
(succ 123 124)
(succ 124 125)
(succ 125 126)
(succ 126 127)
(succ 127 128)
(succ 128 129)
(succ 129 130)
(succ 130 131)
(succ 131 132)
(succ 132 133)
(succ 133 134)
(succ 134 135)
(succ 135 136)
(succ 136 137)
(succ 137 138)
(succ 138 139)
(succ 139 140)
(succ 140 141)
(succ 141 142)
(succ 142 143)
(succ 143 144)
(succ 144 145)
(succ 145 146)
(succ 146 147)
(succ 147 148)
(succ 148 149)
(succ 149 150)
(succ 150 151)
(succ 151 152)
(succ 152 153)
(succ 153 154)
(succ 154 155)
(succ 155 156)
(succ 156 157)
(succ 157 158)
(succ 158 159)
(succ 159 160)
(succ 160 161)
(succ 161 162)
(succ 162 163)
(succ 163 164)
(succ 164 165)
(succ 165 166)
(succ 166 167)
(succ 167 168)
(succ 168 169)
(succ 169 170)
(succ 170 171)
(succ 171 172)
(succ 172 173)
(succ 173 174)
(succ 174 175)
(succ 175 176)
(succ 176 177)
(succ 177 178)
(succ 178 179)
(succ 179 180)
(succ 180 181)
(succ 181 182)
(succ 182 183)
(succ 183 184)
(succ 184 185)
(succ 185 186)
(succ 186 187)
(succ 187 188)
(succ 188 189)
(succ 189 190)
(succ 190 191)
(succ 191 192)
(succ 192 193)
(succ 193 194)
(succ 194 195)
(succ 195 196)
(succ 196 197)
(succ 197 198)
(succ 198 199)
(succ 199 200)
(succ 200 201)
